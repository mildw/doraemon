plugins {
	id 'org.springframework.boot' version '2.6.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
	id 'java'
}

repositories {
	mavenCentral()
}

dependencies {

	implementation project(':core')

	// For Local Test
	implementation 'org.springframework.cloud:spring-cloud-starter-function-web:3.1.6'
	// Spring Cloud Function
	implementation 'org.springframework.cloud:spring-cloud-function-adapter-aws:3.1.6'

	implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
	implementation 'com.amazonaws:aws-lambda-java-events:3.11.0'

	implementation 'org.springframework.boot:spring-boot-starter-jdbc'

	testImplementation 'junit:junit:4.13.2'

	// Database
	implementation 'org.mariadb.jdbc:mariadb-java-client:2.4.1'

	// Jpa
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Jackson-ObjectMapper
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.6'

	// Lombok
	compileOnly 'org.projectlombok:lombok:1.18.24'
	annotationProcessor 'org.projectlombok:lombok:1.18.24'

	// Excel
	implementation 'org.apache.poi:poi:4.1.2'
	implementation 'org.apache.poi:poi-ooxml:4.1.2'

	// QueryDsl
	implementation 'com.querydsl:querydsl-jpa:5.0.0'
	implementation 'com.querydsl:querydsl-apt:5.0.0'

	// AWS
	implementation platform('software.amazon.awssdk:bom:2.17.162')
	implementation 'software.amazon.awssdk:s3'
	implementation 'software.amazon.awssdk:s3-transfer-manager:2.17.123-PREVIEW'
	// RDS proxy
	implementation 'io.awspring.cloud:spring-cloud-starter-aws-secrets-manager-config:2.4.4'

}

task buildZip(type: Zip) {
	from compileJava
	from processResources
	into('lib') {
		from configurations.runtimeClasspath
	}
}

def querydslDir = "$buildDir/generated/querydsl"

querydsl {
	jpa = true
	querydslSourcesDir = querydslDir
}
sourceSets {
	main.java.srcDir querydslDir
}
compileQuerydsl {
	options.annotationProcessorPath = configurations.querydsl
}
configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	querydsl.extendsFrom compileClasspath
}

sourceCompatibility = 11
targetCompatibility = 11
